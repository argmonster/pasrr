' Display list of PASRR Identified events on system for selected client
' Allow staff to create a 'final' event to be used by billing to bill for total hours
'
' Script Name: PASRR Events
' Author: M.Lusk
' Center STARCARE Lubbock
' version: 0.0
' Date: 6/11/2013
'
' Notes: 
'  The data area is a bit wide to allow for a scrollbar.
'
' Updates:
'  tim 0.1 6/18 - 28/2013
'     + parmfile & option support
'     + defualts
'        ~ centralized default configuration
'     ~ moved most hardcoded options to use get parmfile / option methodology
'     + enhanced event face to face check
'     + option to call external script for final event creation
'     + option to call external script for displaying Medicaid Fair Hearing Denial Letters
'
'  tim 0.1.0.1 7/30/2013
'     + added email notifcation on event creation dependant on allowstaffnotify option
'
'define the default values

%define d_fromdate "05/01/2013"
%define d_MIBill 6811   
%define d_MINoBill 0 
%define d_MIFinal 6810  
%define d_IDDBill 6813  
%define d_IDDNoBill 0
%define d_IDDFinal 6812 

%define row_color1 "bgcolor='Gray'"

%version 0.1.0.1 7/30/2013 TxAce
start PASRREvent(parmfile, option, client, retcode)

parmfile             is x
option               is x
client               is x
retcode              is b
                                              
' -------- Assignments  ------------------
iClientId            is x
staff_name[]         is x
Estaffid[]           is x
'-- fund source
c.el                 is h     'DST-90 Client Eligibility Record
c.el.fs              is b     'DST-98 Funding Source Number
c.el.ef.dt           is d     'DST-93 Eligibility Effective Date
c.el.la.dt           is d     'DST-94 Eligibility Lapse Date
'--
staffid              is x     'for Event creation
e.date               is x     'for Event creation
e.staff              is x     'for Event creation
 
e.case.no            is x     'for Event creation
e.start              is t     'for Event creation
e.ru                 is b     'for Event creation
e.dur                is t     'for Event creation
e.ser                is x     'for Event creation
e.loc                is x     'for Event creation
e.recip              is x     'for Event creation
e.att                is x     'for Event creation
createEvent          is x  createEvent = "Y"
index                is i 
ELoc[]               is x
EAttend[]            is x
ERecip[]             is x
ColWidth[]           is x
staffName            is x 
c.medicaid           is x    'DST for Medicaid ??? 
clientName           is x
c.fn                 is x
c.ln                 is x
s.ln                 is x
s.fn                 is x
s.ru                 is b 
EDate[]              is d
ETime[]              is t
ESer[]               is b
EDur[]               is t
ERu[]                is b
Color                is b
ESerDis              is x
bCounter             is n
FromDate             is d 
ThruDate             is d 
Total_MIBill         is t
Total_MINoBill       is t
Total_IDDBill        is t
Total_IDDNoBill      is t 
fmEBalance           is x     'formatted EBalance   "HHH:MM"
fmEDur               is x     'formated EDur        "HHH:MM"

'---First Billable Contact---  Recip Code 1, 2, 5, or 6
firstContact         is x    firstContact = "Y"    
firstRecip           is x
firstStaff           is x
firstDate            is d
firstRU              is b
firstStart           is t
firstLoc             is x
firstAtt             is x
'---Events---
PASRR_type           is x     'IS this MI or IDD?
SVC_MIBill           is b     'MI Billable
SVC_MINoBill         is b     'MI Non-Billable
SVC_MIFinal          is b     'MI Final
SVC_IDDBill          is b     'IDD Billable
SVC_IDDNoBill        is b     'IDD Non-Billable
SVC_IDDFinal         is b     'IDD Final

SVC_totals[]         is t
svc_style[]          is x
svc_style_other      is x

'---Options---
styles[]             is x
row_color[]          is x

show_fs              is x
fs_dsts[]            is x
fs_style[]           is x
fs_colwidth[]        is x

ev_colwidth[]        is x

f2frecip             is x
f2fatt               is x
f2floc               is x

usc_addevent         is x
usc_option           is x
usc_letter           is x

allowcreateevent     is x
allowNotifyStaff     is x
'---
 
rc                   is b 
continue             is x  continue = "Y"
Exit_btn             is x     'Exit button
Add_Eventbtn         is x     'Add PASRR MI/IDD Events button
temp[]               is x
 
string               is x     'error message
row-counter          is b

msg_staffBtn         is x
email_billstaff      is x
email_sub            is x
email_msg            is x

letter_btn           is x

'--- Script Init
'--- Center Service Code assignment
SVC_MIBill = d_MIBill            'MI Billable, Service Code 271
SVC_MINoBill = d_MINoBill        'MI Non-Billable, Service Code 276
SVC_MIFinal = d_MIFinal          'MI Final, Service Code 259
 
SVC_IDDBill = d_IDDBill          'IDD Billable, Service Code 279
SVC_IDDNoBill = d_IDDNoBill      'IDD Non-Billable, Service Code 280
SVC_IDDFinal = d_IDDFinal        'IDD Final, Service Code 297
 
f2frecip = "1,4,5,6"
f2fatt = "1,2,3"
f2floc = "1,2,3,4,5,6,7,9"


'--- Date Range 
FromDate =  d_fromdate  '-- At present there is no real beginning date 
ThruDate = $today

'form styling
styles[1] = ".sb1|font-family:Arial;font-size:12pt;color:red;font-weight:bold"
styles[2] = ".sb2|font-family:Arial;font-size:10pt;color:darkblue;font-weight:bold"
styles[3] = ".sb3|font-family:Arial;font-size:10pt;color:black;font-weight:bold"   
styles[4] = ".sb4|font-family:Arial;font-size:10pt;color:darkblue;font-weight:regular"
styles[5] = ".sb5|font-family:Arial;font-size:10pt;color:red;font-weight:regular"    
styles[6] = ".sb6|font-family:Arial;font-size:10pt;color:green;font-weight:regular"  
styles[7] = ".sb7|font-family:Arial;font-size:10pt;color:white;font-weight:regular"  
styles[8] = ".sb9|font-family:Arial;font-size:12pt;color:darkblue;font-weight:bold"
styles[9] = ".sb10|font-family:Arial;font-size:10pt;color:gray;font-weight:bold"
styles[10] = ".sb11|font-family:Arial;font-size:16pt;color:gray;font-weight:bold;text-align:center"
styles[11] = ".sb12|font-family:Arial;font-size:12pt;color:gray;font-weight:bold"
styles[12] = ".sb14|font-family:Arial;font-size:10pt;color:purple;font-weight:regular"
styles[13] = ".sb15|font-family:Arial;font-size:10pt;color:#5c1919;font-weight:regular"
styles[14] = ".sb16|font-family:Arial;font-size:10pt;color:gray;font-weight:regular"
styles[15] = ".box|margin: .5%; padding: .5%;border-style: solid; border-width: 1px; width: 98%; z-index: 3; text-align: center;"
styles[16] = "body|text-align:center;margin:0;padding:0"

svc_style[svc_mibill] = "sb4"
svc_style[svc_minobill] = "Sb6"
svc_style[svc_mifinal] = "sb14"
svc_style[svc_iddbill] = "sb4"
svc_style[svc_iddNoBill] = "sb6"
svc_style[svc_iddfinal] = "sb14"
svc_style_other = "sb5"

row_color[0] = "bgcolor='white'"
row_color[1] = "bgcolor='whitesmoke'"

fs_dsts[1] = "c.el"
fs_dsts[2] = "c.el.fs"
fs_dsts[3] = "c.el.ef.dt"
fs_dsts[4] = "c.el.la.dt"
fs_dsts[5] = "c.medicaid"

fs_style[1] = "FSInfo"
fs_style[3] = "border='0' width='800'"
fs_ColWidth[1] = "30%"
fs_ColWidth[2] = "20%"
fs_ColWidth[3] = "30%"
fs_ColWidth[4] = "20%" 

ev_ColWidth[1] = "80"
ev_ColWidth[2] = "70"
ev_ColWidth[3] = "260"
ev_ColWidth[4] = "320"
ev_ColWidth[5] = "70"
ev_ColWidth[6] = "70"
ev_ColWidth[7] = "70" 
ev_ColWidth[8] = "70"
ev_ColWidth[9] = "90" 

allowcreateevent = "Y"
allownotifystaff = "Y"

usc_addevent = "PASRRBilling"
usc_option = "allowNoFlags`Y;RunMode`P"

usc_letter = "PASRRLetter"

email_billstaff = "donnag@gcmhmr.com,deborahw@gcmhmr.com,sharonb@gcmhmr.com"
email_sub = "#PASRR Evaluation Completed"
email_msg = "PASRR Evaluation completed for %CID%"

getparm(parmfile)
getoption(option)

f2frecip = "," + f2frecip + ","
f2fatt = "," + f2fatt + ","
f2floc = "," + f2floc + ","

Begin:
if client !dp          
   '%include inc_GetClientID   'Returning iClientID and $regid
   return
else 
   iClientID = client
endif


$clear(fmEBalance,string,Total_MIBill,Total_MINoBill,Total_IDDBill,Total_IDDNoBill)

(void)$dbread(2, iClientID, c.ln, c.fn)
clientName = $nc(c.fn) + " " + $nc(c.ln)
 

$looplimit = 0
'--- Using old Style Styles  (Convert to CSS)
bcounter = 0
do while bcounter++ < $maxarray(styles[])
   (void)$getds(temp[], styles[bcounter], "|")
   $setstyle(temp[1], temp[2]) ', temp[3], temp[4], temp[5],temp[6],temp[7], temp[8], temp[9])
enddo

$submitopt("Off","")
$cancelopt("Off","")


MainArea:
'--- Title area ---
 

$form("Main")
$ctag("<div class='box' id='TitleGroup'>")
' $stylesheet("http://zambia.lubbockmhmr.local/cmhcbui/cmhcbui/styles/LRMHMR.css")
 
$text("PASRR Review Process","sb11")

$ctag("<div style='text-align: center'>")
$br(1)
$text(" Client: ","sb10") $text(iClientID,"sb10") $text(clientName,"sb10")
$br(2)

if show_fs != "N" then
   
   '(void)$dbRead(2,iClientID,c.el,c.el.fs,c.el.ef.dt,c.el.la.dt,c.medicaid)
   (void)$dbRead(2,iClientID,c.el,c.el.fs,c.el.ef.dt,c.el.la.dt)
   
   $clear(colwidth[])
   colwidth[] = fs_colwidth[]
   $table(fs_style[1], fs_style[2], fs_style[3])
      $row()
         $col("Right",,ColWidth[1]) $text("Fund Souce Number:","sb10")
         $col("Left",,ColWidth[2]) $text(c.el.fs,"sb10")
         $col("Right",,ColWidth[3]) $text("Medicaid Number:","sb10")
         $col("Left",,ColWidth[4])  $text(c.medicaid,"sb10")
      $row()
         $col("Right",,ColWidth[1]) $text("Fund Source Eligibility Date:","sb10")
         $col("Left",,ColWidth[2]) $text(c.el.ef.dt,"sb10")
         $col("Right",,ColWidth[3]) 
         $col("Left",,ColWidth[4]) 
      $row()
         $col("Right",,ColWidth[1]) $text("Fund Source Lapse Date:","sb10")
         $col("Left",,ColWidth[2]) $text(c.el.la.dt,"sb10")
         $col("Right",,ColWidth[3]) 
         $col("Left",,ColWidth[4]) 
   $endtable("MiscInfo")
    
endif

$ctag("</div>")  

'{"lib_STANDARD"}MaxDisplay()

'--- Outer border ---
$br(1)
 
  $ctag("<div class='box' id='Main'>")
 
'--- PASRR Header ---
$ctag("<div id='Header'>")
 
'$ctag("&nbsp;") $text("PASRR Events ","sb12") 
$br(1)
ColWidth[1] = "90"
ColWidth[2] = "77"
ColWidth[3] = "263"
ColWidth[4] = "330"
ColWidth[5] = "69"
ColWidth[6] = "67"
ColWidth[7] = "60" 
ColWidth[8] = "63"
ColWidth[9] = "81" 
$table("Header",,"border='1' width='98%'")
   $row(,row_color1)
      $col("Center",,ColWidth[1]) $text("DATE","sb7")
      $col("Center",,ColWidth[2]) $text("TIME","sb7")
      $col("Center",,ColWidth[3]) $text("SERVICE","sb7")
      $col("Center",,ColWidth[4]) $text("STAFF","sb7")
      $col("Center",,ColWidth[5]) $text("RU","sb7")
      $col("Center",,ColWidth[6]) $text("LOC","sb7")
      $col("Center",,ColWidth[7]) $text("ATTEND","sb7")
      $col("Center",,ColWidth[8]) $text("RECIP","sb7")
      $col("Center",,ColWidth[9]) $text("DURATION","sb7")
$endtable("Header")

$ctag("</div>")

'--- PASRR Data ---

'--- Put events into arrays
$ctag("<div class='box' id='Data'>")
 
(void)$ertoarray(,iClientID,FromDate,ThruDate,EStaffID[],,,EDate[],ETime[],client[],,,ESer[],EDur[],,,ERu[],,ELoc[],,,,,,,,,EAttend[],ERecip[])
 
(void)$sort(EDate[], "A", ETime[],ESer[],EStaffID[],EDur[],ERu[],Eloc[],EAttend[],ERecip[])

$table("Data",,"border='1' width='98%'")
bCounter = 0
row-counter = 0 
do while bCounter++ < $maxarray(EDate[])
   ' Exclude events other than PASRR codes
 
   select ESer[bCounter]
 
      case SVC_MIBill 
        or SVC_MINoBill 
        or SVC_MIFinal 
        or SVC_IDDBill 
        or SVC_IDDNoBill 
        or SVC_IDDFinal       goto Do_Calc

      case other              goto Skip_Calc
 
   endselect
 
   Do_Calc:
   
   row-counter++
   
   '-- Is this the 1st billable contact?
   if  firstContact = "Y" then
      
      if $find(ERecip[bCounter], f2fRecip, 1, "F") > 0 
      and $find(EAttend[bCounter], f2fAtt, 1, "F") > 0
      and $find(Eloc[bCounter], f2fLoc, 1, "F") > 0 then 'found the first f2f
         firstRecip = ERecip[bCounter]
         firstStaff = EStaffID[bCounter]
         firstDate = EDate[bCounter]
         firstRU = ERu[bCounter]
         firstStart = ETime[bCounter]
         firstLoc = Eloc[bCounter]
         firstAtt = EAttend[bCounter]
         firstContact = "N"
      endif
    
   endif

   '-- Only allow 1 set of final events in a series
   if ESer[bCounter] = SVC_MIFinal
   or ESer[bCounter] = SVC_IDDFinal then
      createEvent = "N"
   endif

   rc = ESer[bCounter]
   svc_totals[rc] += EDur[bCounter]
   if svc_style[rc] !dp then svc_style[rc] = svc_style_other endif
   $style(svc_style[rc])

   color = $mod(row-counter, 2)
   $row(,row_color[color])

   ESerDis = " - " + $SAM(ESer[bCounter],"D") 
   (void)$dbread(3,EStaffID[bCounter],s.fn,s.ln)
   staffName = $nc(s.fn) + " " + $nc(s.ln)
   fmEDur = $format(EDur[bCounter],"HHH:MM")
   
   colwidth[] = ev_colwidth[]
   $col("right",,ColWidth[1]) $text(EDate[bCounter])
   $col("right",,ColWidth[2]) $text(ETime[bCounter])    
   $col("left",,ColWidth[3])  $text(ESer[bCounter]) $text(ESerDis) 
   $col("left",,ColWidth[4])  $text(EStaffID[bCounter]) $text(" - ")   $text(staffName)
   $col("left",,ColWidth[5])  $text(ERU[bCounter])
   $col("left",,ColWidth[6])  $text(ELoc[bCounter])
   $col("left",,ColWidth[7])  $text(EAttend[bCounter])
   $col("left",,ColWidth[8])  $text(ERecip[bCounter])
   $col("right",,ColWidth[9]) $text(fmEDur)
    
   Skip_Calc:
enddo

$endtable("Data")
$ctag("</div>")
 
'--- PASRR Balances ---
$ctag("<div class='box' id='Events'>")
 
ColWidth[1] = "600"
ColWidth[2] = "200" 
ColWidth[3] = "50" 
ColWidth[4] = "200" 
ColWidth[5] = "50" 
$table("PASRRBal",,"border='0' width='98%'")
   $row()
      $col("left","bottom",ColWidth[1],,"2")
         if allowCreateEvent = "Y" and createEvent = "Y" then
            $text("Sum Premiminary Events and create a Final Billable Event.","sb10")
            $submit(Add_Eventbtn, "Add Event")
         elseif allowNotifyStaff = "Y" and createEvent = "Y" then
            $text("Notify Billing Staff that PE is Complete", "sb10")
            $submit(msg_staffBtn, "email")
         else
            $text("Final Billable Events have been created.","sb10")
         endif
         $col("right",,ColWidth[2]) 
            $text("MI Billable Total:","sb4")
            $br(1)
            $text("MI Non-Billable Total:  ","sb6")  
         $col("right",,ColWidth[3]) 
            fmEBalance = $format(svc_totals[svc_MIBill],"HHH:MM")
             
            $text(fmEBalance,"sb4")
            $br(1)
            fmEBalance = $format(svc_Totals[svc_MINoBill],"HHH:MM")
             
            $text(fmEBalance,"sb6")
       
         $col("right",,ColWidth[4]) 
            $text("IDD Billable Total:  ","sb16") 
            $br(1)
            $text("IDD Non-Billable Total:  ","sb15")
         $col("right",,ColWidth[5]) 

            fmEBalance = $format(svc_Totals[svc_IDDBill],"HHH:MM")
            $text(fmEBalance,"sb16")
             
            fmEBalance = $format(svc_Totals[svc_IDDNoBill],"HHH:MM")
            $text(fmEBalance,"sb15")
$endtable("PASRRBal")
 
$ctag("</div>")

'$ctag("</div>")
'--- end Balance section ---


'--- warning ---
$ctag("<div id='Warning'>") 
   if svc_totals[svc_MIBill] > 6:00 or svc_Totals[svc_IDDBill] > 6:00 then
      $text("The maximum billable hours has been exceeded.","sb5")
   endif
$ctag("</div>")
'------


'--- print/end section ---
$ctag("<div id='Bottom'>") 
   $br(1)
   %include c_pbutton
   if usc_letter dp then
      $submit(letter_btn, "Print MFH Letter")
   endif
   $br(2)
   $submit(Exit_btn,"Exit")
$ctag("</div>")

'close the main div
$ctag("</div>")
'------

$endform("Main")
$sendform("Main")

'--- Start Form Loop (Cycle Until Selection) ---
do while continue = "Y"
 
   if Exit_btn = "Y"      'Exit button
      continue = "N"
       
      goto EndScript
       
   endif
   if Add_Eventbtn = "Y"   'Add MI/IDD Events button
      if allowNotifyStaff = "Y" then
         email_msg = $replace("%CID%", iClientID, email_msg)
         (void)$email(email_billstaff, email_sub, email_msg)
      endif
      goto AddEvent
   endif
   if letter_btn = "Y" then
      $clear(letter_btn)
      call usc_letter(parmfile, option, iClientID, retcode)
      goto Begin
   endif
   if msg_staffBtn = "Y" then
      continue = "N"
      email_msg = $replace("%CID%", iClientID, email_msg)
      if $email(email_billstaff, email_sub, email_msg) > 0 then
         'error msg
      endif
   endif
enddo

return

'--- Add PASRR Final Event
AddEvent:

if usc_addevent dp then
   '(void)$getds(temp[], script_addevent, "|")
   'call temp[1](temp[2], temp[3], temp[4], temp[5], temp[6])
   call usc_addevent (parmfile, usc_option, iClientId, retcode)
else
   '-- Assignments based upon 1st billable contact
   e.case.no = iClientID
   e.recip = firstRecip         
   e.staff = firstStaff          
   e.date = firstDate           
   e.ru = firstRU             
   e.start = firstStart         
   e.loc = firstLoc           
   e.att = firstAtt        
    
   '-- MI Event
   if Total_MIBill
      dp then 
      if Total_MIBill > 6:00 then
         '6:00 cap
         e.dur = 6:00
      else
      
         e.dur = Total_MIBill
      endif
      e.ser = SVC_MIFinal
    
      rc = $insertevent(e.case.no,e.staff,e.date,e.ru,e.start,e.loc,e.dur,e.ser,e.recip,e.att)
    
      if rc != 0 then
         string = "An error has occured adding the PASRR MI Event. " + "RC Error = " + rc
         $brmsg(string, 1, "WC")
      endif
   endif
   
   '-- IDD Event
   if Total_IDDBill dp then
      if Total_IDDBill > 6:00 then
         '6:00 cap
         e.dur = 6:00
      else
      
         e.dur = Total_IDDBill
      endif
    
      e.ser = SVC_IDDFinal
       
      rc = $insertevent(e.case.no,e.staff,e.date,e.ru,e.start,e.loc,e.dur,e.ser,e.recip,e.att)
    
      if rc != 0 then
         string = "An error has occured adding the PASRR IDD Event. " + "RC Error = " + rc
         $brmsg(string, 1, "WC")
      endif
   endif
    
   if rc = 0 then
      goto Begin
      'refresh
   else
      continue = "N"
   endif
endif

EndScript:

end PASRREvent

%include inc_GetParm
%include inc_GetOption